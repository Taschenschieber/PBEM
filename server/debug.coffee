# (c) 2014 Stephan Hillebrand
#
# This file provides some useful functions for debugging, like seeding random
# data for testing purposes.
#
# If the "debug" switch in config.coffee is set to off/no/false, this whole file
# is omitted and does not add any functionality. 

config = require "./config"
database = require "./database"

unless config.debug
  # functions need to exist to prevent crashes due to nil-pointers.
  exports.setupRoutes = (app) ->
    console.log "Debugging mode is OFF."
    
else
  exports.setupRoutes = (app) -> 
    console.log "WARNING: Debugging is ON. Never use these settings in production!"
    app.get "/debug/seed/users", (req, res) ->
      createRandomUser 1000, 0, () ->
        res.send "DONE"
        
    app.get "/debug/seed/games", (req,res) ->
      database.User.find({}).exec (err, users) ->
        console.log err if err
        database.Scenario.find({}).exec (err, scenarios) ->
          sc = 0
          userA = 0
          userB = 1
          for i in [0..1000]
            game = new database.Game
              playerA: users[userA]?.name
              playerB: users[userB]?.name
              scenario: scenarios[sc]?._id
              result: randomResult()
              whoIsAttacker: if Math.random() < 0.5 then "A" else "B"
              
            sc++
            sc = 0 if sc >= scenarios.length
            
            userA+=2
            userB+=2
            if userA >= users.length
              userA = 0
              userB = 1
            if userB >= users.length
              userB = 0
              
            game.save (err) ->
              console.log err if err
        res.send "[DONE]"
              
  randomResult = () ->
    results = ["ongoing", "winA", "winB"]
    number = Math.floor(Math.random()*3)
    return results[number]
      
  # NOTE: Obviously, no users generated by this function should be existent
  # on any production systems.  
  createRandomUser = (count, loopCount, callback) ->
    user = new database.User
      name: "test"+loopCount
      password: "test" # generic login
      email: ""
      activated: true
      # e-mail notification won't work, obviously
     
    user.save (err) ->
      console.log err if err
      console.log loopCount
      if loopCount < count
        createRandomUser count, loopCount+1, callback
      else
        callback()
      
  
    